group 'venus'
version '0.1.0'

buildscript {
    ext.kotlin_version = '1.2.60'
    ext.dokka_version = '0.9.16'
    ext.ktlint_version = '0.27.0'

    repositories {
        //maven { url 'https://dl.bintray.com/kotlin/kotlin-1.1.4' }
        maven { url "https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib/$kotlin_version" }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.dokka'

runDceKotlinJs.keep 'venus_main.venus.glue.Driver.run', 'venus_main.venus.assembler.Linter'

configurations {
    ktlint
}

repositories {
    //maven { url 'https://dl.bintray.com/kotlin/kotlin-1.1.4' }
    //maven { url "https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib/$kotlin_version" }
    mavenCentral()
    jcenter()
}

task ktlint(type: JavaExec) {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "--verbose", "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "doc"
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}
